buildscript {
	repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
	}

    dependencies {
        classpath(
                "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.5.3",
                "com.github.ben-manes:gradle-versions-plugin:0.13.0",
                "gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.4",
                "net.ltgt.gradle:gradle-apt-plugin:0.21",
                "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3",
        )
    }
}

plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'com.jfrog.bintray' version '1.8.4'
    id "signing"
    id "maven-publish"
}

group = 'com.clarifai'
version = '7.3.0'

repositories {
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.google.protobuf:protobuf-java:3.12.0'
    api 'com.google.protobuf:protobuf-java-util:3.12.0'
    api 'io.grpc:grpc-protobuf:1.29.0'
    api 'io.grpc:grpc-stub:1.29.0'
    api 'io.grpc:grpc-netty-shaded:1.29.0'
    api 'javax.annotation:javax.annotation-api:1.3.2'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'com.squareup.okhttp3:okhttp:4.6.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

jar{
    baseName = 'clarifai-grpc'
}

// Huge thanks to Cedric Beust for documenting these arcane and confusing options here:
// http://beust.com/weblog/2015/07/13/the-long-and-arduous-road-to-jcenter-and-maven-bliss/
def clarifai = [
  orgName: "Clarifai",
  description: "Clarifai Java API Client",
  github: "https://github.com/Clarifai/clarifai-java",
]

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadoc(type: Javadoc, overwrite: true) {
  source = sourceSets.main.allSource
  classpath += configurations.compile + configurations.compileOnly
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  //noinspection GroovyAccessibility
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

bintray{
    user = System.getenv("BINTRAY_USERNAME")
    key = System.getenv("BINTRAY_API_KEY")
    publications = ['ClarifaiGrpcRelease']

    publish = true
    configurations = ['archives']

    pkg{
        userOrg = 'clarifai'
        repo = 'Clarifai'
        name = 'ClarifaiGrpc'
        description = 'Clarifai Java gRPC Client'

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Clarifai/clarifai-java-grpc'
        version{
            name = project.version
            released = new Date()
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

publishing{
    publications{
        ClarifaiGrpcRelease(MavenPublication){
            from components.java
            groupId project.group
            artifactId 'clarifai-grpc'
            version project.version
            artifact javadocJar
            artifact sourcesJar

            pom {
                name 'ClarifaiGrpc'
                description 'Clarifai Java gRPC Client'
                url "https://github.com/Clarifai/clarifai-java-grpc"
                inceptionYear "2021"

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    url "https://github.com/Clarifai/clarifai-java-grpc"
                    connection "https://github.com/Clarifai/clarifai-java-grpc.git"
                }
                developers {
                    developer {
                        name "Clarifai, Inc."
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // This is an arbitrary name, you may also use "mavencentral" or
            // any other name that's descriptive for you
            name = "sonatype"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv('SONATYPE_USERNAME_CLARIFAI')
                password = System.getenv('SONATYPE_PASSWORD_CLARIFAI')
            }
        }
    }
}
